plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.15.0'
	id 'com.github.davidmc24.gradle.plugin.avro' version '1.9.1'
}

group = 'org.example.oms'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url = uri("https://packages.confluent.io/maven/") }
}

dependencies {
    // Spring WebFlux for reactive web
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.7'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.28'
    
    // RSocket for bidirectional streaming
    implementation 'org.springframework.boot:spring-boot-starter-rsocket'
    
    // Kafka for event consumption
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'io.projectreactor.kafka:reactor-kafka'
    
    // Confluent Avro serialization
    implementation 'io.confluent:kafka-avro-serializer:7.9.0'
    implementation 'org.apache.avro:avro:1.12.0'
    
    // JSON processing
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    
    // Actuator for health and metrics
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    
    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    
    // Logging
    implementation 'org.slf4j:slf4j-api'
    
    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

configurations {
    avroTools {
        transitive = false
    }
}

task openApiGenerateCmd(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/openapi/oms-cmd-api.yml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "org.example.common.api"
    modelPackage = "org.example.common.model.cmd"
    configOptions = [
        interfaceOnly: "true",
        useSpringBoot3: "true",
        skipDefaultInterface: "true",
        reactive: "true",
		enumPropertyNaming: "original",
        modelNameSuffix: "",
		additionalModelTypeAnnotations: "@lombok.experimental.SuperBuilder @lombok.extern.jackson.Jacksonized"
    ]
}

task openApiGenerateQuery(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/openapi/oms-query-api.yml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "org.example.common.api"
    modelPackage = "org.example.common.model.query"
    configOptions = [
        interfaceOnly: "true",
        useSpringBoot3: "true",
        skipDefaultInterface: "true",
        reactive: "true",
		enumPropertyNaming: "original",
        modelNameSuffix: "",
		additionalModelTypeAnnotations: "@lombok.experimental.SuperBuilder @lombok.extern.jackson.Jacksonized"
    ]
}

task openApiGenerateAvro(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "avro-schema"
    inputSpec = "$projectDir/src/main/openapi/oms-cmd-api.yml".toString()
    outputDir = "$buildDir/generated-avro".toString()
    configOptions = [
        packageName: "org.example.common.model.msg"
    ]
}

tasks.named("compileJava") {
    dependsOn tasks.named("openApiGenerateCmd")
    dependsOn tasks.named("openApiGenerateQuery")
    dependsOn tasks.named("openApiGenerateAvro")
}

// Add the generated sources to the source set
sourceSets.main.java.srcDirs += "$buildDir/generated/src/main/java"

test {
    useJUnitPlatform()
}
